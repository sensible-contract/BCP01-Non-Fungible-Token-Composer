import "util.scrypt";

/**
 * A safty NFT protocol payload parser
 *
 * PayloadNFT负责解码/编码nft合约的数据。构造函数解码，dump函数编码。
 *
 * bsv合约可以由脚本代码部分和数据部分组成，两部分之间用 OP_RETURN 操作符隔开。
 * 当锁定脚本执行到OP_RETURN时便会终止，其后的数据部分不会被处理。
 * 但当前数据部分依然是一条或多条正常的OP_PUSHDATAX语句，附带着确定数量的字节内容。
 *
 * NFT合约的数据格式设计如下：
 *
 * 最后一个字节为数据格式类型，分为4种：ISSUE、TRANSFER、SWAP、SELL
 *
 * 1. codePart 为合约代码。结尾还包括2字节额外内容：OP_RETURN操作符 和 OP_PUSHDATA(一个字节0x28)，表示接下来有40字节的GenesisPart。
 * 2. GenesisPart 为合约溯源数据。包括genesisTx的 某utxo的outpoint，以及创建合约的outputIndex。共 32+4+4=40 字节。
 * 3. 其后再包括一个OP_PUSHDATA(dataPrefix),可能有多字节，取决于其他数据data的长度。
 * 4. 接下来便是其他数据部分聚合，比如：pubkeyHash有20字节，tokenId有8字节，satoshiAmount有8字节。
 *
 *  codePart, GenesisPart, issuerPkh, tokenId, ISSUE
 *  codePart, GenesisPart, ownerPkh, tokenId, TRANSFER
 *  codePartA, GenesisPartA, ownerPkhA, tokenId, hash256(codePartB + GenesisPartB), tokenAmountB, SWAP
 *  codePart, GenesisPart, ownerPkh, tokenId, satoshiAmount, SELL
 */

struct PayloadNFTData {
  /* 合约最后一块数据部分开始的字节下标，在第二个OP_PUSHDATA之后 */
  int dataStart;

  /* 合约代码的内容，末尾包括 OP_RETURN 和 0x28 */
  bytes codePart;

  /* 包括合约代码内容，OP_RETURN 和 0x28，再加上GenesisPart内容，但不包括后续的 OP_PUSHDATA */
  bytes codeWithGenesisPart;

  /* 数据类型，1字节 */
  bytes dataType;

  /* 溯源需匹配genesisTx input的outpoint 和output的index */
  bytes genesisOutpoint;
  bytes genesisOutputIdx;

  /* 数据 */
  bytes   metaTxId;
  Ripemd160 ownerPkh;
  int totalSupply;
  int tokenId;
 

  /* swap 数据 */
  Sha256 codeWithGenesisPartHashSwap;
  int amountSwap;

  /* sell 数据 */
  int satoshiAmountSell;
}

library PayloadNFT {

  /* 不同类型数据部分的长度 */
  static int genesisLen = 40;
  static int issueLen = 37;
  static int transferLen = 61;

  /* OP_PUSHDATA 协议推送不同长度数据时，需要的前缀字节 */
  static bytes issuePrefix = b'25';
  static bytes transferPrefix = b'3d';

  /* 数据类型dataType标记 */
  static bytes ISSUE = b'00';
  static bytes TRANSFER = b'01';

  static function load(bytes scriptCode) : PayloadNFTData {
    PayloadNFTData self = {
      0,                        // int dataStart;
      b'00',                    // bytes codePart;
      b'00',                    // bytes codeWithGenesisPart;
      b'00',                    // bytes dataType;
      b'00',                    // bytes genesisOutpoint;
      b'00',                    // bytes genesisOutputIdx;
      b'00',                    // bytes meta txid;
      Ripemd160(b'00'),         // Ripemd160 ownerPkh;
      0,                        // int totalSupply;
      0,                        // int tokenId;
      Sha256(b'00'),            // Sha256 codeWithGenesisPartHashSwap;
      0,                        // int amountSwap;
      0                         // int satoshiAmountSell;
    };

    int scriptLen = len(scriptCode);
    self.dataType = scriptCode[scriptLen - 1 : ];

    int dataLen = 0;
    int prefixLen = 0;
    if (self.dataType == PayloadNFT.ISSUE){
      dataLen = PayloadNFT.issueLen;
      prefixLen = len(PayloadNFT.issuePrefix);
    }else if(self.dataType == PayloadNFT.TRANSFER){
      dataLen = PayloadNFT.transferLen;
      prefixLen = len(PayloadNFT.transferPrefix);
    }

    /* part */
    self.dataStart = len(scriptCode) - dataLen;
    self.codeWithGenesisPart = scriptCode[ : self.dataStart - prefixLen];
    int codeWithGenesisLen = len(self.codeWithGenesisPart);
    self.codePart = self.codeWithGenesisPart[ : codeWithGenesisLen - PayloadNFT.genesisLen];
    /* genesis */
    self.genesisOutpoint = self.codeWithGenesisPart[codeWithGenesisLen - PayloadNFT.genesisLen : codeWithGenesisLen - 4];
    self.genesisOutputIdx = self.codeWithGenesisPart[codeWithGenesisLen - 4 : ];
    /* data */
    bytes dataPart = scriptCode[self.dataStart : ];

    /* all data types */
   
    self.ownerPkh = Ripemd160(dataPart[ :  20]);
    self.tokenId = unpack(dataPart[20 : 20 + 8]);
    
    if(self.dataType == PayloadNFT.ISSUE){
      self.totalSupply = unpack(dataPart[28 : 28 + 8]);
    }else if (self.dataType == PayloadNFT.TRANSFER) {
      self.metaTxId = dataPart[28 : 28 + 32];
    }

    return self;
  }

  static function dump(PayloadNFTData self) : bytes {
    bytes payload = b'';
    if(self.dataType == PayloadNFT.ISSUE){
      payload = PayloadNFT.issuePrefix + 
      self.ownerPkh +
      num2bin(self.tokenId, Util.OutputValueLen) + 
      num2bin(self.totalSupply, Util.OutputValueLen) + 
      self.dataType;
    }else if(self.dataType == PayloadNFT.TRANSFER){
      payload = PayloadNFT.transferPrefix + 
      self.ownerPkh +
      num2bin(self.tokenId, Util.OutputValueLen) +
      self.metaTxId +
      self.dataType;
    } 
    return self.codeWithGenesisPart + payload;
  }
}
