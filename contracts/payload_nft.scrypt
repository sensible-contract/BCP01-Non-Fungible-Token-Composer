import "util.scrypt";

/**
 * A safty NFT protocol payload parser
 *
 * PayloadNFT负责解码/编码nft合约的数据。构造函数解码，dump函数编码。
 *
 * bsv合约可以由脚本代码部分和数据部分组成，两部分之间用 OP_RETURN 操作符隔开。
 * 当锁定脚本执行到OP_RETURN时便会终止，其后的数据部分不会被处理。
 * 但当前数据部分依然是一条或多条正常的OP_PUSHDATAX语句，附带着确定数量的字节内容。
 *
 * NFT合约的数据格式设计如下：
 *
 * 最后一个字节为数据格式类型，分为4种：ISSUE、TRANSFER、SWAP、SELL
 *
 * 1. codePart 为合约代码。结尾还包括2字节额外内容：OP_RETURN操作符 和 OP_PUSHDATA(一个字节0x28)，表示接下来有40字节的GenesisPart。
 * 2. GenesisPart 为合约溯源数据。包括genesisTx的 某utxo的outpoint，以及创建合约的outputIndex。共 32+4+4=40 字节。
 * 3. 其后再包括一个OP_PUSHDATA(dataPrefix),可能有多字节，取决于其他数据data的长度。
 * 4. 接下来便是其他数据部分聚合，比如：pubkeyHash有20字节，tokenId有8字节，satoshiAmount有8字节。
 *
 *  codePart, GenesisPart, issuerPkh, tokenId, ISSUE
 *  codePart, GenesisPart, ownerPkh, tokenId, TRANSFER
 *  codePartA, GenesisPartA, ownerPkhA, tokenId, hash256(codePartB + GenesisPartB), tokenAmountB, SWAP
 *  codePart, GenesisPart, ownerPkh, tokenId, satoshiAmount, SELL
 */

contract PayloadNFT {
  /* 不同类型数据部分的长度 */
  static const int genesisLen = 40;
  static const int issueLen = 37;
  static const int transferLen = 61;

  /* OP_PUSHDATA 协议推送不同长度数据时，需要的前缀字节 */
  static const bytes issuePrefix = b'25';
  static const bytes transferPrefix = b'3d';

  /* 数据类型dataType标记 */
  static const bytes ISSUE = b'00';
  static const bytes TRANSFER = b'01';

  /* 合约最后一块数据部分开始的字节下标，在第二个OP_PUSHDATA之后 */
  int dataStart;

  /* 合约代码的内容，末尾包括 OP_RETURN 和 0x28 */
  bytes codePart;

  /* 包括合约代码内容，OP_RETURN 和 0x28，再加上GenesisPart内容，但不包括后续的 OP_PUSHDATA */
  bytes codeWithGenesisPart;

  /* 数据类型，1字节 */
  bytes dataType;

  /* 溯源需匹配genesisTx input的outpoint 和output的index */
  bytes genesisOutpoint;
  bytes genesisOutputIdx;

  /* 数据 */
  bytes metaTxId;
  Ripemd160 ownerPkh;
  int totalSupply;
  int tokenId;

  /* swap 数据 */
  Sha256 codeWithGenesisPartHashSwap;
  int amountSwap;

  /* sell 数据 */
  int satoshiAmountSell;

  constructor(bytes scriptCode) {
    int scriptLen = len(scriptCode);
    this.dataType = scriptCode[scriptLen - 1 : ];

    int dataLen = 0;
    int prefixLen = 0;
    if (this.dataType == ISSUE) {
      dataLen = issueLen;
      prefixLen = len(issuePrefix);
    }
    else if (this.dataType == TRANSFER) {
      dataLen = transferLen;
      prefixLen = len(transferPrefix);
    }

    /* part */
    this.dataStart = len(scriptCode) - dataLen;
    this.codeWithGenesisPart = scriptCode[ : this.dataStart - prefixLen];
    int codeWithGenesisLen = len(this.codeWithGenesisPart);
    this.codePart = this.codeWithGenesisPart[ : codeWithGenesisLen - genesisLen];
    /* genesis */
    this.genesisOutpoint = this.codeWithGenesisPart[codeWithGenesisLen - genesisLen : codeWithGenesisLen - 4];
    this.genesisOutputIdx = this.codeWithGenesisPart[codeWithGenesisLen - 4 : ];
    /* data */
    bytes dataPart = scriptCode[this.dataStart : ];

    /* all data types */

    this.ownerPkh = Ripemd160(dataPart[ : 20]);
    this.tokenId = Util.fromLEUnsigned(dataPart[20 : 20 + 8]);

    if (this.dataType == ISSUE) {
      this.totalSupply = Util.fromLEUnsigned(dataPart[28 : 28 + 8]);
    }
    else if (this.dataType == TRANSFER) {
      this.metaTxId = dataPart[28 : 28 + 32];
    }
  }

  public function setTokenId(int tokenId) {
    this.tokenId = tokenId;
    require(true);
  }

  public function setMetaTxId(bytes metaTxId) {
    this.metaTxId = metaTxId;
    require(true);
  }

  public function setOwnerPkh(Ripemd160 ownerPkh) {
    this.ownerPkh = ownerPkh;
    require(true);
  }

  public function setDataType(bytes dataType) {
    this.dataType = dataType;
    require(true);
  }

  function dump() : bytes {
    bytes payload = b'';
    if (this.dataType == ISSUE) {
      payload = issuePrefix +
      this.ownerPkh +
      num2bin(this.tokenId, Util.OutputValueLen) +
      num2bin(this.totalSupply, Util.OutputValueLen) +
      this.dataType;
    }
    else if (this.dataType == TRANSFER) {
      payload = transferPrefix +
      this.ownerPkh +
      num2bin(this.tokenId, Util.OutputValueLen) +
      this.metaTxId +
      this.dataType;
    }
    return this.codeWithGenesisPart + payload;
  }
}
